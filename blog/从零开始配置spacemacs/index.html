<!DOCTYPE html>
<html lang="en-us">
<head>
  <title>从零开始配置Spacemacs - 捉妖记</title>
  <meta charset="utf-8" />
  <meta name="author" content="lijigang &lt;i@lijigang.com&gt;" />

  <link rel="alternate" title="RSS Feed" href="/rss.xml" type="application/rss+xml">
  <link rel="stylesheet" href="/media/css/main.css" type="text/css">
  <link rel="stylesheet" href="/media/css/posts.css" type="text/css">
</head>

  <body class="container">
<header id="header">
<body>
    <nav class="navbar navbar-default navbar-fixed-top" style="opacity: .9" role="navigation">
        <div class="container-fluid">
<div class="navbar-header">
            <button type="button" class="navbar-toggle" data-toggle="collapse" data-target="#bs-example-navbar-collapse-1">
                <span class="sr-only">Toggle navigation</span>
                <span class="icon-bar"></span>
                <span class="icon-bar"></span>
                <span class="icon-bar"></span>
            </button>
            <a class="navbar-brand" href="/">捉妖记</a>
        </div>
<div class="collapse navbar-collapse" id="bs-example-navbar-collapse-1">
            <ul class="nav navbar-nav navbar-right">
                <li class="active"><a href="/">Blog</a></li>
          <li><a href="https://github.com/lijigang">GitHub</a></li>
          <li><a href="http://www.zhihu.com/people/lijigang.com">知乎</a></li>
        </ul>
</div>
</div>
    </nav>
</body>
</header>

<section id="content" role="main">
    <div id="outline-container-sec-" class="row post-content">
        <div class="col-md-2"></div>
            <h1>从零开始配置Spacemacs</h1>
            <p>
在我看来，在编辑操作领域，没有比Vim更高效的操作方式了。在文档存储和知识管理相关的软件使用上面，没有比Org-Mode更牛逼的存在了。
</p>

<p>
之前一直很纠结，既放不下Vim的高效编辑方式，又不放下Emacs里的Org-mode。直到遇到了Spacemacs：
</p>

<blockquote>
<p>
Spacemacs = Vim + Emacs
</p>
</blockquote>

<p>
这篇文章，就记录一下自己是怎么从零开始配置一个符合自己使用习惯的Spacemacs。
</p>

<div id="outline-container-orgc2a766a" class="outline-2">
<h2 id="orgc2a766a"><span class="section-number-2">1</span> 安装</h2>
<div class="outline-text-2" id="text-1">
<ol class="org-ol">
<li>安装 <a href="https://emacsformacosx.com/">Emacs</a></li>
<li>Clone Spacemacs</li>
</ol>
<pre class="example">
git clone https://github.com/syl20bnr/spacemacs ~/.emacs.d
</pre>

<p>
clone完成后，会多两个东西，
</p>
<ul class="org-ul">
<li><code>~/.emacs.d/</code></li>
<li><code>~/.spacemacs</code></li>
</ul>
</div>
</div>

<div id="outline-container-org4c7466d" class="outline-2">
<h2 id="org4c7466d"><span class="section-number-2">2</span> 配置</h2>
<div class="outline-text-2" id="text-2">
<p>
安装完成后，对于常规的配置，可以放置到 <code>~/.spacemacs</code> 文件中，就像Vim和Zsh一样。 但Spacemacs提供了 <a href="http://spacemacs.org/doc/LAYERS.html">layer</a> 概念，极大方便了插件的管理.
</p>

<p>
下面分为两部分来配置， 首先把一些简单的配置放置到 <code>~/.spacemacs</code> 文件中：
</p>
</div>

<div id="outline-container-org8ca1c81" class="outline-3">
<h3 id="org8ca1c81"><span class="section-number-3">2.1</span> .spacemacs</h3>
<div class="outline-text-3" id="text-2-1">
<ul class="org-ul">
<li><p>
layers
</p>

<p>
在 <code>dotspacemacs-configuration-layers</code> 变量中增加你想配置的layer(Spacemacs出厂自带的或者是你后续自己创建的）
</p></li>
</ul>
<div class="org-src-container">
<pre class="src src-Emacs-lisp">dotspacemacs-configuration-layers
'(
  helm
  auto-completion
  better-defaults
  emacs-lisp
  git
  org
  )
</pre>
</div>

<ul class="org-ul">
<li>themes
可以从 <a href="https://themegallery.robdor.com/">这里</a> 挑选你喜欢的主题，把名字放到配置的第一个即可。</li>
</ul>
<div class="org-src-container">
<pre class="src src-emacs-lisp">dotspacemacs-themes '(monokai
                      molokai
                      niflheim)
</pre>
</div>

<ul class="org-ul">
<li>fullscreen</li>
</ul>
<div class="org-src-container">
<pre class="src src-emacs-lisp">dotspacemacs-fullscreen-at-startup t
</pre>
</div>

<p>
在dotspacemacs/user-config函数中配置：
</p>
<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun dotspacemacs/user-config ()
  "Configuration function for user code.
This function is called at the very end of Spacemacs initialization after
layers configuration.
This is the place where most of your configurations should be done. Unless it is
explicitly specified that a variable should be set before a package is loaded,
you should place your code here."

  ;;;;;;;;;;;;;;;;;;;;;;;
  ;; org-mode 相关设置 ;;
  ;;;;;;;;;;;;;;;;;;;;;;;

  ;; Since version 0.104, spacemacs uses the org version from the org ELPA
  ;; repository instead of the one shipped with emacs. Then, any org related
  ;; code should not be loaded before dotspacemacs/user-config, otherwise both
  ;; versions will be loaded and will conflict. Because of autoloading, calling
  ;; to org functions will trigger the loading up of the org shipped with emacs
  ;; which will induce conflicts. One way to avoid conflict is to wrap your org
  ;; config code in a with-eval-after-load block like this:
  (with-eval-after-load 'org
    ;; 设置todo keywords
    (setq org-todo-keywords
          '((sequence "TODO" "HAND" "|" "DONE")))

    ;; 调试好久的颜色，效果超赞！ todo keywords 增加背景色
    (setf org-todo-keyword-faces '(("TODO" . (:foreground "white" :background "#95A5A6"   :weight bold))
                                   ("HAND" . (:foreground "white" :background "#2E8B57"  :weight bold))
                                   ("DONE" . (:foreground "white" :background "#3498DB" :weight bold))))

    ;; 设置bullet list
    (setq org-bullets-bullet-list '("☰" "☷" "☯" "☭"))

    ;; 打开org-indent mode
    (setq org-startup-indented t)

    (setq org-agenda-include-diary nil)
    (setq org-agenda-files (list  "/Users/ljg/Library/Mobile Documents/com~apple~CloudDocs/org/gtd.org"
                                  "/Users/ljg/Library/Mobile Documents/com~apple~CloudDocs/org/gtd.org_archive"))

    (setq org-default-notes-file "/Users/ljg/Library/Mobile Documents/com~apple~CloudDocs/org/gtd.org")
    (setq org-refile-targets '("~/Library/Mobile Documents/com~apple~CloudDocs/org/gtd.org" :maxlevel . 3))
    )

  ;;;;;;;;;;;;;;
  ;; 显示相关 ;;
  ;;;;;;;;;;;;;;

  ;; 在状态栏显示时间
  (display-time-mode 1)

  ;; 折叠时不再显示「...」
  (setq org-ellipsis "▼")

  ;; inline image 不用展示实际大小，可以自定义大小显示
  (setq org-image-actual-width nil)

  (global-hl-line-mode -1)

  ;; more useful frame title, that show either a file or a
  ;; buffer name (if the buffer isn't visiting a file)
  (setq frame-title-format
        '("" " 為學日益, 為道日損 - "
          (:eval (if (buffer-file-name)
                     (abbreviate-file-name (buffer-file-name)) "%b"))))
  ;;;;;;;;;;;;;;
  ;; 导出相关 ;;
  ;;;;;;;;;;;;;;

  ;; 安装XeLaTeX是另外一个故事了..
  (setq Tex-command-default "XeLaTeX")


  ;;;;;;;;;;;;;;
  ;; 其它杂项 ;;
  ;;;;;;;;;;;;;;

  (setq default-major-mode 'org-mode)

  (prefer-coding-system 'utf-8)
  (set-default-coding-systems 'utf-8)
  (setq default-buffer-file-coding-system 'utf-8)

  ;; Agenda clock report parameters
  (setq org-agenda-clockreport-parameter-plist
        '(:link t :maxlevel 6 :fileskip0 t :compact t :narrow 60 :score 0))

  (setq org-hierarchical-todo-statistics nil)

  ;; stop emacs asking for confirmation when eval source code
  (setq org-confirm-babel-evaluate nil)

  ;; active Org-babel languages
  (org-babel-do-load-languages
   'org-babel-load-languages
   '(;; other Babel languages
     (plantuml . t)))

  (setq org-plantuml-jar-path
        (expand-file-name "~/lib/plantuml.jar"))
  )
</pre>
</div>
</div>
</div>

<div id="outline-container-org1a38b56" class="outline-3">
<h3 id="org1a38b56"><span class="section-number-3">2.2</span> private layer</h3>
<div class="outline-text-3" id="text-2-2">
<p>
首先把自己layer中想要的packages给列好, 并设置相应的启动配置项:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defconst lijigang-packages
  '(org-page
    cnfonts
    swiper
    wttrin
    beacon)
)

(defun lijigang/init-org-page()
  "Initialize org-page to publish blog."
  (use-package org-page
    :ensure t
    :config (progn
          (setq op/site-main-title "為學日益, 為道日損")
          (setq op/personal-github-link "https://github.com/lijigang")
          (setq op/repository-directory "~/lijigang")
          (setq op/site-domain "http://lijigang.github.io/")
          (setq op/theme-root-directory "~/.emacs.d/elpa/org-page-20170806.1924/themes")
          (setq op/theme 'ljg)
          (setq op/highlight-render 'js)
          (setq op/category-config-alist
            '(("blog"
              :show-meta t
              :show-comment t
              :uri-generator op/generate-uri
              :uri-template "/blog/%t/"
              :sort-by :date     ;; how to sort the posts
              :category-index nil) ;; generate category index or not
              ("index"
              :show-meta nil
              :show-comment nil
              :uri-generator op/generate-uri
              :uri-template "/"
              :sort-by :date
              :category-index nil)
              ("about"
              :show-meta nil
              :show-comment nil
              :uri-generator op/generate-uri
              :uri-template "/about/"
              :sort-by :date
              :category-index nil)))
          (bind-key "C-c M-p" 'op/do-publication-and-preview-site)))
    )

(defun lijigang/init-cnfonts()
  "Initialize cnfonts"
  (use-package cnfonts
    :init
    (cnfonts-enable)
    (cnfonts-set-spacemacs-fallback-fonts)))

(defun lijigang/init-swiper()
  "Initialize swiper"
  (use-package swiper
    :init
    (define-key global-map (kbd "C-s") 'swiper)))


(defun lijigang/init-wttrin()
  (use-package wttrin
    :ensure t
    :commands (wttrin)
    :init
    (setq wttrin-default-cities '("Beijing"
                                  "Bristol")))
  )

(defun lijigang/init-beacon()
  "Initialize beacon"
  (use-package beacon
    :init
    (beacon-mode 1)
    (setq beacon-color "#666600")))

;;; packages.el ends here

</pre>
</div>

<p>
然后把自己定义的一些函数放到 <code>funcs.el</code> 文件:
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(defun make-progress (width percent has-number?)
  (let* ((done (/ percent 100.0))
         (done-width (floor (* width done))))
    (concat
     "["
     (make-string done-width ?/)
     (make-string (- width done-width) ? )
     "]"
     (if has-number? (concat " " (number-to-string percent) "%"))
     )))

(defun insert-day-progress ()
  (interactive)
  (let* ((today (time-to-day-in-year (current-time)))
         (percent (floor (* 100 (/ today 365.0)))))
    (insert (make-progress 30 percent t))
    ))

;; latex支持中文
(require 'ox)
(require 'ox-html)
(require 'ox-publish)

(add-to-list 'org-latex-classes '("pdf" "\\documentclass[fontset = mac]{ctexart}
[NO-DEFAULT-PACKAGES]
\\usepackage[colorlinks,linkcolor=black,anchorcolor=black,
             citecolor=black]{hyperref}
\\usepackage[top=3truecm,bottom=2.5truecm,
            left=1.1truecm,right=1.1truecm,
            bindingoffset=1.0truecm,
            headsep=1.6truecm,
            footskip=1.5truecm,
            headheight=15pt    % 标准中没有要求页眉的高度，这里设置成
                               % 15pt了
           ]{geometry}
"
                  ("\\section{%s}" . "\\section*{%s}")
                  ("\\subsection{%s}" . "\\subsection*{%s}")
                  ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                  ("\\paragraph{%s}" . "\\paragraph*{%s}")
                  ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))

(setq org-latex-default-class "pdf")

(setq org-latex-pdf-process
      '(
        "xelatex -interaction nonstopmode -output-directory %o %f"
        "xelatex -interaction nonstopmode -output-directory %o %f"
        "xelatex -interaction nonstopmode -output-directory %o %f"
        "rm -fr %b.out %b.log %b.tex auto"
        ))

(defun peng-use-xelatex ()
  (interactive)
  (let* ((tempfile
      (file-name-base))) (progn (shell-command (concat "rm -rf " tempfile
                               ".bbl " tempfile ".blg " tempfile ".out " tempfile ".log " tempfile
                               ".aux " tempfile ".toc" tempfile ".pdf"))
                    (compile (concat "xelatex "
                             (concat tempfile ".tex")
                             (concat ";rm -rf " tempfile ".bbl " tempfile
                                 ".blg " tempfile ".out " tempfile ".log " tempfile ".aux " tempfile
".toc" ";open " tempfile ".pdf"))))))

</pre>
</div>

<p>
之后把自己使用的一些快捷键设置放到 <code>keybindings.el</code> :
</p>

<div class="org-src-container">
<pre class="src src-emacs-lisp">(evil-leader/set-key "oc" 'org-capture)
(evil-leader/set-key "oa" 'org-agenda)
(evil-leader/set-key "ol" 'org-store-link)
(evil-leader/set-key "el" 'eval-print-last-sexp)
(evil-leader/set-key "od" 'org-archive-subtree)

(evil-leader/set-key "oip" 'org-set-property)
(evil-leader/set-key "oil" 'org-insert-link)
(evil-leader/set-key "ois" 'org-time-stamp)
(evil-leader/set-key "oid" 'org-insert-drawer)
(evil-leader/set-key "oif" 'org-footnote-action)

(evil-leader/set-key "oog" (lambda () (interactive) (find-file "~/Library/Mobile Documents/com~apple~CloudDocs/org/gtd.org")))
(evil-leader/set-key "ool" (lambda () (interactive) (find-file "/Users/ljg/.spacemacs.d/layers/lijigang/keybindings.el")))

(evil-leader/set-key "oit" 'insert-day-progress)

(global-set-key (kbd "C--") 'org-table-insert-hline)

(global-set-key
 (kbd "C-c p")
 (defhydra hydra-blog (:color blue :hint nil)
   "
    blog  _n_: new post                        _l_: publish last commit
          _r_: reset &amp; publish all             _p_: publish interactively
          _t_: reset &amp; publish to /tmp/blog    _e_: new-repository
          "
   ("n" op/new-post)
   ("r" (progn
          (setq op/item-cache nil)
          (op/do-publication t nil nil t t)))
   ("t" (progn
          (setq op/item-cache nil)
          (op/do-publication t "/tmp/blog" nil t nil)))
   ("l" (op/do-publication nil "HEAD~1" nil t t))
   ("p" op/do-publication)
   ("e" op/new-repository)))

</pre>
</div>
</div>
</div>
</div>

    </div>
</section>


<footer id="footer">
    <br />
</footer>

  </body>
</html>
